module pslhdsa

// WOTS+ chaining function
//
// Algorithm 5 chain(ğ‘‹, ğ‘–, ğ‘ , PK.seed, ADRS)
//
// Chaining function used in WOTS+.
// Input: Input string ğ‘‹, start index ğ‘–, number of steps ğ‘ , public seed PK.seed, address ADRS.
// Output: Value of F iterated ğ‘  times on ğ‘‹.
// (where ğ‘– + ğ‘  < w
fn chain(c Context, x []u8, i int, s int, pk_seed []u8, addr_ Address) ![]u8 {
	assert x.len == c.n
	if i + s >= w {
		return error('Invalid wots+ params')
	}
	mut addr := addr_.clone()
	mut tmp := x.clone()
	for j := i; j < i + s; j++ {
		// ADRS.setHashAddress(ğ‘—)
		addr.set_hash_address(u32(j))
		// ğ‘¡ğ‘šğ‘ â† F(PK.seed, ADRS,ğ‘¡ğ‘šğ‘)
		tmp = c.f(pk_seed, addr, tmp)!
	}
	return tmp
}

// 5.1 WOTS+ Public-Key Generation
// Algorithm 6 wots_pkGen(SK.seed, PK.seed, ADRS)
// Generates a WOTS+ public key.
// Input: Secret seed SK.seed, public seed PK.seed, address ADRS.
// Output: WOTS+ public key ğ‘k
fn wots_pkgen(c Context, sk_seed []u8, pk_seed []u8, addr_ Address) ![]u8 {
	assert addr_.get_type()! == .wots_hash
	// copy address to create key generation key address
	mut addr := addr_.clone()
	mut sk_addr := addr.clone()
	// skADRS.setTypeAndClear(WOTS_PRF)
	sk_addr.set_type_and_clear(.wots_prf)
	// skADRS.setKeyPairAddress(ADRS.getKeyPairAddress())
	sk_addr.set_keypair_address(addr.get_keypair_address())

	// gets wotsp length from the current context
	wots_len := c.wots_len()
	// temporary buffer to store output
	mut tmp := []u8{}
	for i := 0; i < wots_len; i++ {
		// skADRS.setChainAddress(ğ‘–)
		sk_addr.set_chain_address(u32(i))
		// compute secret value for chain i, ğ‘ ğ‘˜ â† PRF(PK.seed, SK.seed, skADRS)
		skey := c.prf(pk_seed, sk_seed, sk_addr)!
		// ADRS.setChainAddress(ğ‘–)
		addr.set_chain_address(u32(i))
		// compute public value for chain ğ‘–, ğ‘¡ğ‘šğ‘[ğ‘–] â† chain(ğ‘ ğ‘˜, 0, ğ‘¤ âˆ’ 1, PK.seed, ADRS)
		tmp_i := chain(c, skey, 0, w - 1, pk_seed, addr)!
		tmp << tmp_i
	}
	// copy address to create WOTS+public key address, wotspkADRS â† ADRS
	mut wots_pk_addr := addr.clone()
	// wotspkADRS.setTypeAndClear(WOTS_PK)
	wots_pk_addr.set_type_and_clear(.wots_pk)
	// wotspkADRS.setKeyPairAddress(ADRS.getKeyPairAddress())
	// TODO: remove int cast
	wots_pk_addr.set_keypair_address(addr.get_keypair_address())
	// compress public key, ğ‘ğ‘˜ â† Tğ‘™ğ‘’ğ‘›(PK.seed, wotspkADRS,ğ‘¡ğ‘šğ‘)
	pk := c.tlen(c.wots_len(), pk_seed, wots_pk_addr, tmp)!

	return pk
}

// 5.2 WOTS+ Signature Generation
// A WOTS+ signature is an array of ğ‘™ğ‘’ğ‘› byte strings of length n
//
// Algorithm 7 wots_sign(ğ‘€, SK.seed, PK.seed, ADRS)
// Generates a WOTS+ signature on an ğ‘›-byte message.
// Input: Message ğ‘€, secret seed SK.seed, public seed PK.seed, address ADRS.
// Output: WOTS+ signature ğ‘ ğ‘–ğ‘”.
fn wots_sign(c Context, m []u8, sk_seed []u8, pk_seed []u8, addr_ Address) ![]u8 {
	mut csum := u64(0)
	mut addr := addr_.clone()
	// convert message to base w, ie, ğ‘šğ‘ ğ‘” â† base_2b(ğ‘€, ğ‘™ğ‘”ğ‘¤, ğ‘™ğ‘’ğ‘›1)
	len1 := c.len1()
	mut msgs := base_2exp_b(m, c.lgw, len1)

	// compute checksum
	for i := 0; i < len1; i++ {
		// ğ‘ğ‘ ğ‘¢ğ‘š â† ğ‘ğ‘ ğ‘¢ğ‘š + ğ‘¤ âˆ’ 1 âˆ’ ğ‘šğ‘ ğ‘”[ğ‘–]
		csum += w - 1 - msgs[i]
	}
	// for ğ‘™ğ‘”ğ‘¤ = 4, left shift by 4, its only values supported in this module
	// ğ‘ğ‘ ğ‘¢ğ‘š â† ğ‘ğ‘ ğ‘¢ğ‘š â‰ª ((8 âˆ’ ((ğ‘™ğ‘’ğ‘›2 â‹… ğ‘™ğ‘”ğ‘¤) mod 8)) mod 8)
	csum <<= 4 // u64((8 - ((len2 * c.lgw) % 8)) % 8)

	// convert to base w, ğ‘šğ‘ ğ‘” â† ğ‘šğ‘ ğ‘” âˆ¥ base_2b (toByte (ğ‘ğ‘ ğ‘¢ğ‘š, âŒˆ(ğ‘™ğ‘’ğ‘›2*ğ‘™ğ‘”ğ‘¤)/8âŒ‰) , ğ‘™ğ‘”ğ‘¤, ğ‘™ğ‘’ğ‘›2)
	// mlen := 2 // cdiv(len2 * c.lgw, 8)
	// dump(mlen)
	// bytes := to_bytes(csum, mlen)
	msgs << base_2exp_b(to_bytes(csum, 2), c.lgw, len2)

	// copy address to create key generation key address
	mut sk_addr := addr.clone()
	// skADRS.setTypeAndClear(WOTS_PRF)
	sk_addr.set_type_and_clear(.wots_prf)
	// skADRS.setKeyPairAddress(ADRS.getKeyPairAddress())
	// TODO: handle int > larger than max_int
	sk_addr.set_keypair_address(u32(addr.get_keypair_address()))

	mut sig := []u8{}
	for i := 0; i < c.wots_len(); i++ {
		// skADRS.setChainAddress(ğ‘–)
		sk_addr.set_chain_address(u32(i))
		// compute chain ğ‘– secret value, ğ‘ ğ‘˜ â† PRF(PK.seed, SK.seed, skADRS)
		skey := c.prf(pk_seed, sk_seed, sk_addr)!
		addr.set_chain_address(u32(i))
		// compute chain ğ‘– signature value, ğ‘ ğ‘–ğ‘”[ğ‘–] â† chain(ğ‘ ğ‘˜, 0, ğ‘šğ‘ ğ‘”[ğ‘–], PK.seed, ADRS)
		sig_i := chain(c, skey, 0, int(msgs[i]), pk_seed, addr)!
		sig << sig_i
	}
	return sig
}

// 5.3 Computing a WOTS+ Public Key From a Signature
//
// Algorithm 8 wots_pkFromSig(ğ‘ ğ‘–ğ‘”, ğ‘€, PK.seed, ADRS)
// Computes a WOTS+ public key from a message and its signature.
// Input: WOTS+ signature ğ‘ ğ‘–ğ‘”, message ğ‘€, public seed
// Output: WOTS+ public key ğ‘ğ‘˜ğ‘ ğ‘–ğ‘” derived from ğ‘ ğ‘–ğ‘”.
fn wots_pkfromsig(c Context, sig []u8, m []u8, pk_seed []u8, addr_ Address) ![]u8 {
	mut csum := u64(0)
	mut addr := addr_.clone()
	// convert message to base w, ie, ğ‘šğ‘ ğ‘” â† base_2b(ğ‘€, ğ‘™ğ‘”ğ‘¤, ğ‘™ğ‘’ğ‘›1)
	len1 := c.len1()
	mut msgs := base_2exp_b(m, c.lgw, len1)

	// compute checksum
	// for ğ‘– from 0 to ğ‘™ğ‘’ğ‘›1 âˆ’ 1 do
	for i := 0; i < len1; i++ {
		// ğ‘ğ‘ ğ‘¢ğ‘š â† ğ‘ğ‘ ğ‘¢ğ‘š + ğ‘¤ âˆ’ 1 âˆ’ ğ‘šğ‘ ğ‘”[ğ‘–]
		csum += (w - 1 - msgs[i])
	}
	// for ğ‘™ğ‘”ğ‘¤ = 4, left shift by 4, its only values supported in this module
	// ğ‘ğ‘ ğ‘¢ğ‘š â† ğ‘ğ‘ ğ‘¢ğ‘š â‰ª ((8 âˆ’ ((ğ‘™ğ‘’ğ‘›2 â‹… ğ‘™ğ‘”ğ‘¤) mod 8)) mod 8)
	csum <<= 4 // u64((8 - ((len2 * c.lgw) % 8)) % 8)

	// convert to base w, ğ‘šğ‘ ğ‘” â† ğ‘šğ‘ ğ‘” âˆ¥ base_2b (toByte (ğ‘ğ‘ ğ‘¢ğ‘š, âŒˆ(ğ‘™ğ‘’ğ‘›2*ğ‘™ğ‘”ğ‘¤)/8âŒ‰) , ğ‘™ğ‘”ğ‘¤, ğ‘™ğ‘’ğ‘›2)
	// mlen := 2 // cdiv(len2 * c.lgw, 8)
	// bytes := to_bytes(csum, mlen)
	msgs << base_2exp_b(to_bytes(csum, 2), c.lgw, len2)

	mut tmp := []u8{}
	for i := 0; i < c.wots_len(); i++ {
		// ADRS.setChainAddress(ğ‘–)
		addr.set_chain_address(u32(i))
		// ğ‘¡ğ‘šğ‘[ğ‘–] â† chain(ğ‘ ğ‘–ğ‘”[ğ‘–], ğ‘šğ‘ ğ‘”[ğ‘–], ğ‘¤ âˆ’ 1 âˆ’ ğ‘šğ‘ ğ‘”[ğ‘–], PK.seed, ADRS)
		x := sig[i * c.n..(i + 1) * c.n]
		next_chain := chain(c, x, int(msgs[i]), int(w - 1 - msgs[i]), pk_seed, addr)!
		assert next_chain.len != 0

		tmp << next_chain
	}
	// copy address to create WOTS+ public key address, wotspkADRS â† ADRS
	mut wots_pk_addr := addr.clone()
	// wotspkADRS.setTypeAndClear(WOTS_PK)
	wots_pk_addr.set_type_and_clear(.wots_pk)
	// wotspkADRS.setKeyPairAddress(ADRS.getKeyPairAddress())
	// TODO: remove int casts ??
	wots_pk_addr.set_keypair_address(addr.get_keypair_address())
	// ğ‘ğ‘˜ğ‘ ğ‘–ğ‘” â† Tğ‘™ğ‘’ğ‘›(PK.seed, wotspkADRS,ğ‘¡ğ‘šğ‘)
	pk_sig := c.tlen(c.wots_len(), pk_seed, wots_pk_addr, tmp)!

	return pk_sig
}

fn wots_csum(c Context, m []u8) u64 {
	mut csum := u64(0)
	t := u32((1 << c.lgw) - 1)

	len1 := c.len1()
	mut msg := base_2exp_b(m, c.lgw, len1)

	// for ğ‘– from 0 to ğ‘™ğ‘’ğ‘›1 âˆ’ 1 do
	for i := 0; i < len1; i++ {
		// ğ‘ğ‘ ğ‘¢ğ‘š â† ğ‘ğ‘ ğ‘¢ğ‘š + ğ‘¤ âˆ’ 1 âˆ’ ğ‘šğ‘ ğ‘”[ğ‘–]
		csum += t - msg[i]
	}

	csum <<= u64((8 - ((len2 * c.lgw) & 7)) & 7)

	return csum
}
