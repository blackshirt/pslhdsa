module pslhdsa

import crypto.sha256
import crypto.sha512
import crypto.sha3 // for shake
import crypto.hmac

// SLH-DSA Context
struct Context {
mut:
	// Chapter 11. Parameters Set
	kind Kind
	n    int
	h    int
	d    int
	hp   int
	a    int
	k    int
	lgw  int = 4
	m    int
	sc   int
	pkb  int
	sig  int
}

fn new_context(k Kind) Context {
	prm := paramset[k.str()]
	mut ctx := Context{
		kind: k
		n:    prm.n
		h:    prm.h
		d:    prm.d
		hp:   prm.hp
		a:    prm.a
		k:    prm.k
		lgw:  prm.lgw // int = 4
		m:    prm.m
		sc:   prm.sc
		pkb:  prm.pkb
		sig:  prm.sig
	}

	return ctx
}

// is_shake tells underlying hash was a shake-family algorithm
@[inline]
fn (c Context) is_shake() bool {
	return c.kind.is_shake()
}

// When ğ‘™ğ‘”ğ‘¤ = 4, ğ‘¤ = 16, ğ‘™ğ‘’ğ‘›1 = 2ğ‘›, ğ‘™ğ‘’ğ‘›2 = 3, and ğ‘™ğ‘’ğ‘› = 2ğ‘› + 3.
// See FIPS 205 p17
const w = 16
const len2 = 3

@[inline]
fn (c Context) len1() int {
	return 2 * c.n
}

@[inline]
fn (c Context) wots_len() int {
	return 2 * c.n + 3
}

const sha256_hash_size = sha256.size

// A mask generation function (MGF) is a cryptographic primitive similar
// to a cryptographic hash function except that while a hash function's
// output has a fixed size, a MGF supports output of a variable length.
@[inline]
fn mgf1_sha256(seed []u8, mlen int) []u8 {
	mut out := []u8{}
	for c := 0; c < cdiv(mlen, sha256_hash_size); c++ {
		mut data := []u8{}
		data << seed
		data << to_bytes(u64(c), 4)
		// seed + to_bytes(c, 4)
		out << sha256.sum256(data)
	}
	return out[..mlen]
}

const sha512_hash_size = sha512.size

@[inline]
fn mgf1_sha512(seed []u8, mlen int) []u8 {
	mut out := []u8{}
	for c := 0; c < cdiv(mlen, sha512_hash_size); c++ {
		mut data := []u8{}
		data << seed
		data << to_bytes(u64(c), 4)
		// seed + to_bytes(c, 4)
		out << sha512.sum512(data)
	}
	return out[..mlen]
}

@[inline]
fn hmac_sha256(seed []u8, data []u8) []u8 {
	out := hmac.new(seed, data, sha256.sum256, sha256.size)
	return out
}

@[inline]
fn hmac_sha512(seed []u8, data []u8) []u8 {
	return hmac.new(seed, data, sha512.sum512, sha512.size)
}

// 4.1 Hash Functions and Pseudorandom Functions
//
// Hğ‘šğ‘ ğ‘”(ğ‘…, PK.seed, PK.root, ğ‘€ ) (ğ”¹ğ‘› Ã— ğ”¹ğ‘› Ã— ğ”¹ğ‘› Ã— ğ”¹âˆ— â†’ ğ”¹ğ‘š) is used to generate the
// sha256_digest of the message to be signed.
fn (mut c Context) h_msg(r []u8, pk_seed []u8, pk_root []u8, m []u8) ![]u8 {
	if c.is_shake() {
		// Hğ‘šğ‘ ğ‘”(ğ‘…, PK.seed, PK.root, ğ‘€ ) = SHAKE256(ğ‘… âˆ¥ PK.seed âˆ¥ PK.root âˆ¥ ğ‘€, 8ğ‘š)
		mut data := []u8{}
		data << r
		data << pk_seed
		data << pk_root
		data << m
		return sha3.shake256(data, c.m)
	}
	// SLH-DSA Using SHA2 for Security Category 1
	//
	// mgf1_sha256(R + pk_seed + sha256(R + pk_seed + pk_root + M)
	// Hğ‘šğ‘ ğ‘”(ğ‘…, PK.seed, PK.root, ğ‘€ ) = MGF1-SHA-256(ğ‘… âˆ¥ PK.seed âˆ¥ SHA-256(ğ‘… âˆ¥ PK.seed âˆ¥ PK.root âˆ¥ ğ‘€ ), ğ‘š)
	if c.kind == .sha2_128f || c.kind == .sha2_128s {
		mut data := []u8{}
		data << r
		data << pk_seed
		data << pk_root
		data << m

		digest := sha256.sum256(data)

		mut seed := []u8{}
		seed << r
		seed << pk_seed
		seed << digest

		return mgf1_sha256(seed, c.m)
	}
	// else .sha2_192f, .sha2_192s, .sha2_256f, .sha2_256s
	// SLH-DSA Using SHA2 for Security Categories 3 and 5
	//
	// Hğ‘šğ‘ ğ‘”, PRF, PRFğ‘šğ‘ ğ‘”, F, H, and Tâ„“ shall be instantiated as follows forthe SLH-DSA-SHA2-192s,
	// SLH-DSA-SHA2-192f, SLH-DSA-SHA2-256s, and SLH-DSA-SHA2-256f parameter sets:
	// Hğ‘šğ‘ ğ‘”(ğ‘…, PK.seed, PK.root, ğ‘€ ) = MGF1-SHA-512(ğ‘… âˆ¥ PK.seed âˆ¥ SHA-512(ğ‘… âˆ¥ PK.seed âˆ¥ PK.root âˆ¥ ğ‘€ ), ğ‘š)
	mut data := []u8{}
	data << r
	data << pk_seed
	data << pk_root
	data << m

	digest := sha512.sum512(data)

	mut seed := []u8{}
	seed << r
	seed << pk_seed
	seed << digest

	return mgf1_sha512(seed, c.m)
}

// PRF(PK.seed, SK.seed, ADRS) (ğ”¹ğ‘› Ã— ğ”¹ğ‘› Ã— ğ”¹32 â†’ ğ”¹ğ‘›) is a PRF that is used to
// generate the secret values in WOTS+ and FORS private keys.
fn (mut c Context) prf(pk_seed []u8, sk_seed []u8, addr Address) ![]u8 {
	if c.is_shake() {
		// PRF(PK.seed, SK.seed, ADRS) = SHAKE256(PK.seed âˆ¥ ADRS âˆ¥ SK.seed, 8ğ‘›)
		mut data := []u8{}
		data << pk_seed
		data << addr.bytes()
		data << sk_seed
		return sha3.shake256(data, c.n)
	}
	// sha2 family,
	// SLH-DSA Using SHA2 for Security Category 1
	// PRF(PK.seed, SK.seed, ADRS) = Truncğ‘›(SHA-256(PK.seed âˆ¥ toByte(0, 64 âˆ’ ğ‘›) âˆ¥ ADRSğ‘ âˆ¥ SK.seed))
	addrs_c := addr.compress()
	if c.kind == .sha2_128f || c.kind == .sha2_128s {
		mut data := []u8{}
		data << pk_seed
		data << to_bytes(0, 64 - c.n)
		data << addrs_c
		data << sk_seed

		digest := sha256.sum256(data)
		return digest[..c.n]
	}
	// SLH-DSA Using SHA2 for Security Categories 3 and 5
	//
	// PRF(PK.seed, SK.seed, ADRS) = Truncğ‘›(SHA-256(PK.seed âˆ¥ toByte(0, 64 âˆ’ ğ‘›) âˆ¥ ADRSğ‘ âˆ¥ SK.seed))
	// Really the same with category 1
	mut data := []u8{}
	data << pk_seed
	data << to_bytes(0, 64 - c.n)
	data << addrs_c
	data << sk_seed

	digest := sha256.sum256(data)
	return digest[..c.n]
}

// PRFğ‘šğ‘ ğ‘”(SK.prf, ğ‘œğ‘ğ‘¡_ğ‘Ÿğ‘ğ‘›ğ‘‘, ğ‘€ ) (ğ”¹ğ‘› Ã— ğ”¹ğ‘› Ã— ğ”¹âˆ— â†’ ğ”¹ğ‘›) is a pseudorandom function
// (PRF) that generates the randomizer (ğ‘…) for the randomized hashing of the message to be
// signed.
fn (mut c Context) prf_msg(sk_prf []u8, opt_rand []u8, msg []u8) ![]u8 {
	if c.is_shake() {
		// PRFğ‘šğ‘ ğ‘”(SK.prf, ğ‘œğ‘ğ‘¡_ğ‘Ÿğ‘ğ‘›ğ‘‘, ğ‘€ ) = SHAKE256(SK.prf âˆ¥ ğ‘œğ‘ğ‘¡_ğ‘Ÿğ‘ğ‘›ğ‘‘ âˆ¥ ğ‘€, 8ğ‘›)
		mut data := []u8{}
		data << sk_prf
		data << opt_rand
		data << msg

		return sha3.shake256(data, c.n)
	}
	// sha2 family
	// SLH-DSA Using SHA2 for Security Category 1
	//
	// PRFğ‘šğ‘ ğ‘”(SK.prf, ğ‘œğ‘ğ‘¡_ğ‘Ÿğ‘ğ‘›ğ‘‘, ğ‘€ ) = Truncğ‘›(HMAC-SHA-256(SK.prf, ğ‘œğ‘ğ‘¡_ğ‘Ÿğ‘ğ‘›ğ‘‘ âˆ¥ ğ‘€ ))
	if c.kind == .sha2_128f || c.kind == .sha2_128s {
		mut data := []u8{}
		data << opt_rand
		data << msg

		digest := hmac_sha256(sk_prf, data)
		return digest[..c.n]
	}
	// SLH-DSA Using SHA2 for Security Categories 3 and 5
	// PRFğ‘šğ‘ ğ‘”(SK.prf, ğ‘œğ‘ğ‘¡_ğ‘Ÿğ‘ğ‘›ğ‘‘, ğ‘€ ) = Truncğ‘›(HMAC-SHA-512(SK.prf, ğ‘œğ‘ğ‘¡_ğ‘Ÿğ‘ğ‘›ğ‘‘ âˆ¥ ğ‘€ ))
	mut data := []u8{}
	data << opt_rand
	data << msg
	digest := hmac_sha512(sk_prf, data)

	return digest[..c.n]
}

// F(PK.seed, ADRS, ğ‘€1) (ğ”¹ğ‘› Ã— ğ”¹32 Ã— ğ”¹ğ‘› â†’ ğ”¹ğ‘›) is a hash function that takes an ğ‘›-byte
// message as input and produces an ğ‘›-byte output.
fn (mut c Context) f(pk_seed []u8, addr Address, m1 []u8) ![]u8 {
	if c.is_shake() {
		// F(PK.seed, ADRS, ğ‘€1) = SHAKE256(PK.seed âˆ¥ ADRS âˆ¥ ğ‘€1, 8ğ‘›)
		mut data := []u8{}
		data << pk_seed
		data << addr.bytes()
		data << m1

		return sha3.shake256(data, c.n)
	}
	// 11.2.1 SLH-DSA Using SHA2 for Security Category 1
	// F(PK.seed, ADRS, ğ‘€1) = Truncğ‘›(SHA-256(PK.seed âˆ¥ toByte(0, 64 âˆ’ ğ‘›) âˆ¥ ADRSğ‘ âˆ¥ ğ‘€1))
	addrs_c := addr.compress()
	if c.kind == .sha2_128s || c.kind == .sha2_128f {
		mut data := []u8{}
		data << pk_seed
		data << to_bytes(0, 64 - c.n)
		data << addrs_c
		data << m1

		digest := sha256.sum256(data)
		return digest[..c.n]
	}
	// SLH-DSA Using SHA2 for Security Categories 3 and 5
	//
	// F(PK.seed, ADRS, ğ‘€1) = Truncğ‘›(SHA-256(PK.seed âˆ¥ toByte(0, 64 âˆ’ ğ‘›) âˆ¥ ADRSğ‘ âˆ¥ ğ‘€1))
	mut data := []u8{}
	data << pk_seed
	data << to_bytes(0, 64 - c.n)
	data << addrs_c
	data << m1

	digest := sha256.sum256(data)
	return digest[..c.n]
}

// H(PK.seed, ADRS, ğ‘€2) (ğ”¹ğ‘› Ã— ğ”¹32 Ã— ğ”¹2ğ‘› â†’ ğ”¹ğ‘›) is a special case of Tâ„“ that takes a
// 2ğ‘›-byte message as input.
fn (mut c Context) h(pk_seed []u8, addr Address, m2 []u8) ![]u8 {
	if c.is_shake() {
		// H(PK.seed, ADRS, ğ‘€2) = SHAKE256(PK.seed âˆ¥ ADRS âˆ¥ ğ‘€2, 8ğ‘›)
		mut data := []u8{}
		data << pk_seed
		data << addr.bytes()
		data << m2

		return sha3.shake256(data, c.n)
	}
	// compressed form used in sha2 hashing routines
	addrs_c := addr.compress()
	// SLH-DSA Using SHA2 for Security Category 1
	//
	// H(PK.seed, ADRS, ğ‘€2) = Truncğ‘›(SHA-256(PK.seed âˆ¥ toByte(0, 64 âˆ’ ğ‘›) âˆ¥ ADRSğ‘ âˆ¥ ğ‘€2))
	if c.kind == .sha2_128f || c.kind == .sha2_128s {
		mut data := []u8{}
		data << pk_seed
		data << to_bytes(0, 64 - c.n)
		data << addrs_c
		data << m2

		digest := sha256.sum256(data)
		return digest[..c.n]
	}
	// SLH-DSA Using SHA2 for Security Categories 3 and 5
	//
	// H(PK.seed, ADRS, ğ‘€2) = Truncğ‘›(SHA-512(PK.seed âˆ¥ toByte(0, 128 âˆ’ ğ‘›) âˆ¥ ADRSğ‘ âˆ¥ ğ‘€2))
	mut data := []u8{}
	data << pk_seed
	data << to_bytes(0, 128 - c.n)
	data << addrs_c
	data << m2

	digest := sha512.sum512(data)
	return digest[..c.n]
}

// Tâ„“(PK.seed, ADRS, ğ‘€â„“) (ğ”¹ğ‘› Ã— ğ”¹32 Ã— ğ”¹â„“ğ‘› â†’ ğ”¹ğ‘›) is a hash function that maps an
// â„“ğ‘›-byte message to an ğ‘›-byte message.
fn (mut c Context) tlen(pk_seed []u8, addr Address, ml []u8) ![]u8 {
	assert ml.len % c.n == 0
	if c.is_shake() {
		// Tâ„“(PK.seed, ADRS, ğ‘€â„“) = SHAKE256(PK.seed âˆ¥ ADRS âˆ¥ ğ‘€â„“, 8ğ‘›)
		mut data := []u8{}
		data << pk_seed
		data << addr.bytes()
		data << ml

		return sha3.shake256(data, c.n)
	}
	// sha2 family,
	//
	// SLH-DSA Using SHA2 for Security Category 1
	// Tâ„“(PK.seed, ADRS, ğ‘€â„“) = Truncğ‘›(SHA-256(PK.seed âˆ¥ toByte(0, 64 âˆ’ ğ‘›) âˆ¥ ADRSğ‘ âˆ¥ ğ‘€â„“))
	addrs_c := addr.compress()
	if c.kind == .sha2_128f || c.kind == .sha2_128s {
		mut data := []u8{}
		data << pk_seed
		data << to_bytes(0, 64 - c.n)
		data << addrs_c
		data << ml

		digest := sha256.sum256(data)
		return digest[..c.n]
	}
	// SLH-DSA Using SHA2 for Security Categories 3 and 5
	//
	// Tâ„“(PK.seed, ADRS, ğ‘€â„“) = Truncğ‘›(SHA-512(PK.seed âˆ¥ toByte(0, 128 âˆ’ ğ‘›) âˆ¥ ADRSğ‘ âˆ¥ ğ‘€â„“))
	mut data := []u8{}
	data << pk_seed
	data << to_bytes(0, 128 - c.n)
	data << addrs_c
	data << ml

	digest := sha512.sum512(data)
	return digest[..c.n]
}
