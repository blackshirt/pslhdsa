// Copyright Â© 2024 blackshirt.
// Use of this source code is governed by an MIT license
// that can be found in the LICENSE file.
//
// WOTS+ module
module pslhdsa

// WOTS+ chaining function
//
// Algorithm 5 chain(ğ‘‹, ğ‘–, ğ‘ , PK.seed, ADRS)
//
// Chaining function used in WOTS+.
// Input: Input string ğ‘‹, start index ğ‘–, number of steps ğ‘ , public seed PK.seed, address ADRS.
// Output: Value of F iterated ğ‘  times on ğ‘‹.
// (where ğ‘– + ğ‘  < w
@[direct_array_access; inline]
fn chain(c &Context, x []u8, i int, s int, pkseed []u8, mut adr Address) ![]u8 {
	assert x.len == c.prm.n
	if i + s >= w {
		return error('Invalid wots+ params')
	}
	mut tmp := x.clone()
	for j := i; j < i + s; j++ {
		// ADRS.setHashAddress(ğ‘—)
		adr.set_hash_address(u32(j))
		// ğ‘¡ğ‘šğ‘ â† F(PK.seed, ADRS,ğ‘¡ğ‘šğ‘)
		tmp = c.f(pkseed, adr, tmp, c.prm.n)!
	}
	return tmp
}

// 5.1 WOTS+ Public-Key Generation
//
// Algorithm 6 wots_pkGen(SK.seed, PK.seed, ADRS)
// Generates a WOTS+ public key.
// Input: Secret seed SK.seed, public seed PK.seed, address ADRS.
// Output: WOTS+ public key ğ‘k
@[direct_array_access; inline]
fn wots_pkgen(c &Context, skseed []u8, pkseed []u8, mut adr Address) ![]u8 {
	assert adr.get_type()! == .wots_hash
	// copy address to create key generation key address
	mut sk_addr := adr.clone()
	// skADRS.setTypeAndClear(WOTS_PRF)
	sk_addr.set_type_and_clear(.wots_prf)
	// skADRS.setKeyPairAddress(ADRS.getKeyPairAddress())
	sk_addr.set_keypair_address(adr.get_keypair_address())

	// gets wotsp length from the current context
	length := c.wots_len()
	// temporary buffer to store output
	mut tmp := [][]u8{len: length}
	for i := 0; i < length; i++ {
		// skADRS.setChainAddress(ğ‘–)
		sk_addr.set_chain_address(u32(i))
		// compute secret value for chain i, ğ‘ ğ‘˜ â† PRF(PK.seed, SK.seed, skADRS)
		sk := c.prf(pkseed, skseed, sk_addr, c.prm.n)!
		// ADRS.setChainAddress(ğ‘–)
		adr.set_chain_address(u32(i))
		// compute public value for chain ğ‘–, ğ‘¡ğ‘šğ‘[ğ‘–] â† chain(ğ‘ ğ‘˜, 0, ğ‘¤ âˆ’ 1, PK.seed, ADRS)
		tmp[i] = chain(c, sk, 0, 15, pkseed, mut adr)!
		// tmp << tmp_i
	}
	// copy address to create WOTS+public key address, wotspkADRS â† ADRS
	mut wots_pkadr := adr.clone()
	// wotspkADRS.setTypeAndClear(WOTS_PK)
	wots_pkadr.set_type_and_clear(.wots_pk)
	// wotspkADRS.setKeyPairAddress(ADRS.getKeyPairAddress())
	// TODO: remove int cast
	wots_pkadr.set_keypair_address(adr.get_keypair_address())
	// compress public key, ğ‘ğ‘˜ â† Tğ‘™ğ‘’ğ‘›(PK.seed, wotspkADRS,ğ‘¡ğ‘šğ‘)
	pk := c.tl(pkseed, wots_pkadr, tmp, c.prm.n)!

	return pk
}

// 5.2 WOTS+ Signature Generation
// A WOTS+ signature is an array of ğ‘™ğ‘’ğ‘› byte strings of length n
//
// Algorithm 7 wots_sign(ğ‘€, SK.seed, PK.seed, ADRS)
// Generates a WOTS+ signature on an ğ‘›-byte message.
// Input: Message ğ‘€, secret seed SK.seed, public seed PK.seed, address ADRS.
// Output: WOTS+ signature ğ‘ ğ‘–ğ‘”.
@[direct_array_access]
fn wots_sign(c &Context, m []u8, skseed []u8, pkseed []u8, mut adr Address) ![][]u8 {
	// get some vars
	length := c.wots_len()
	len1 := c.wots_len1()
	len2 := c.wots_len2()
	// convert message to base w, ie, ğ‘šğ‘ ğ‘” â† base_2b(ğ‘€, ğ‘™ğ‘”ğ‘¤, ğ‘™ğ‘’ğ‘›1)
	mut msg := base_2b(m, c.prm.lgw, len1)
	mut csum := u64(0)
	// compute checksum
	for i := 0; i < len1; i++ {
		// ğ‘ğ‘ ğ‘¢ğ‘š â† ğ‘ğ‘ ğ‘¢ğ‘š + ğ‘¤ âˆ’ 1 âˆ’ ğ‘šğ‘ ğ‘”[ğ‘–]
		csum += w - 1 - msg[i]
	}
	// for ğ‘™ğ‘”ğ‘¤ = 4, left shift by 4, its only values supported in this module
	// ğ‘ğ‘ ğ‘¢ğ‘š â† ğ‘ğ‘ ğ‘¢ğ‘š â‰ª ((8 âˆ’ ((ğ‘™ğ‘’ğ‘›2 â‹… ğ‘™ğ‘”ğ‘¤) mod 8)) mod 8)
	csum <<= 4 // u64((8 - ((len2 * c.prm.lgw) % 8)) % 8)

	// convert to base w, ğ‘šğ‘ ğ‘” â† ğ‘šğ‘ ğ‘” âˆ¥ base_2b (toByte (ğ‘ğ‘ ğ‘¢ğ‘š, âŒˆ(ğ‘™ğ‘’ğ‘›2*ğ‘™ğ‘”ğ‘¤)/8âŒ‰) , ğ‘™ğ‘”ğ‘¤, ğ‘™ğ‘’ğ‘›2)
	// mlen := 2 // cdiv(len2 * c.prm.lgw, 8)
	mlen := ((len2 * c.prm.lgw) + 7) >> 3
	// mlen := 2
	bytes := to_byte(csum, mlen)
	msg << base_2b(bytes, c.prm.lgw, len2)

	// copy address to create key generation key address
	mut sk_addr := adr.clone()
	// skADRS.setTypeAndClear(WOTS_PRF)
	sk_addr.set_type_and_clear(.wots_prf)
	// skADRS.setKeyPairAddress(ADRS.getKeyPairAddress())
	// TODO: handle int > larger than max_int
	sk_addr.set_keypair_address(adr.get_keypair_address())

	mut sig := [][]u8{len: length}
	for i := 0; i < length; i++ {
		// skADRS.setChainAddress(ğ‘–)
		sk_addr.set_chain_address(u32(i))
		// compute chain ğ‘– secret value, ğ‘ ğ‘˜ â† PRF(PK.seed, SK.seed, skADRS)
		sk := c.prf(pkseed, skseed, sk_addr, c.prm.n)!
		// ADRS.setChainAddress(ğ‘–)
		adr.set_chain_address(u32(i))
		// compute chain ğ‘– signature value, ğ‘ ğ‘–ğ‘”[ğ‘–] â† chain(ğ‘ ğ‘˜, 0, ğ‘šğ‘ ğ‘”[ğ‘–], PK.seed, ADRS)
		sig[i] = chain(c, sk, 0, int(msg[i]), pkseed, mut adr)!
		// sig << sig_i
	}
	return sig
}

// 5.3 Computing a WOTS+ Public Key From a Signature
//
// Algorithm 8 wots_pkFromSig(ğ‘ ğ‘–ğ‘”, ğ‘€, PK.seed, ADRS)
// Computes a WOTS+ public key from a message and its signature.
// Input: WOTS+ signature ğ‘ ğ‘–ğ‘”, message ğ‘€, public seed
// Output: WOTS+ public key ğ‘ğ‘˜ğ‘ ğ‘–ğ‘” derived from ğ‘ ğ‘–ğ‘”.
@[direct_array_access; inline]
fn wots_pkfromsig(c &Context, sig []u8, m []u8, pkseed []u8, mut adr Address) ![]u8 {
	mut csum := u64(0)
	// convert message to base w, ie, ğ‘šğ‘ ğ‘” â† base_2b(ğ‘€, ğ‘™ğ‘”ğ‘¤, ğ‘™ğ‘’ğ‘›1)
	length := c.wots_len()
	len1 := c.wots_len1()
	len2 := c.wots_len2()

	mut msg := base_2b(m, c.prm.lgw, len1)

	// compute checksum
	// for ğ‘– from 0 to ğ‘™ğ‘’ğ‘›1 âˆ’ 1 do
	for i := 0; i < len1; i++ {
		// ğ‘ğ‘ ğ‘¢ğ‘š â† ğ‘ğ‘ ğ‘¢ğ‘š + ğ‘¤ âˆ’ 1 âˆ’ ğ‘šğ‘ ğ‘”[ğ‘–]
		csum += (w - 1 - msg[i])
	}
	// for ğ‘™ğ‘”ğ‘¤ = 4, left shift by 4, its only values supported in this module
	// ğ‘ğ‘ ğ‘¢ğ‘š â† ğ‘ğ‘ ğ‘¢ğ‘š â‰ª ((8 âˆ’ ((ğ‘™ğ‘’ğ‘›2 â‹… ğ‘™ğ‘”ğ‘¤) mod 8)) mod 8)
	csum <<= 4 // u64((8 - ((len2 * c.prm.lgw) % 8)) % 8)

	// convert to base w, ğ‘šğ‘ ğ‘” â† ğ‘šğ‘ ğ‘” âˆ¥ base_2b (toByte (ğ‘ğ‘ ğ‘¢ğ‘š, âŒˆ(ğ‘™ğ‘’ğ‘›2*ğ‘™ğ‘”ğ‘¤)/8âŒ‰) , ğ‘™ğ‘”ğ‘¤, ğ‘™ğ‘’ğ‘›2)
	// mlen := 2 // cdiv(len2 * c.prm.lgw, 8)
	// bytes := to_byte(csum, mlen)
	msg << base_2b(to_byte(csum, 2), c.prm.lgw, len2)

	mut tmp := [][]u8{len: length}
	for i := 0; i < c.wots_len(); i++ {
		// ADRS.setChainAddress(ğ‘–)
		adr.set_chain_address(u32(i))
		// ğ‘¡ğ‘šğ‘[ğ‘–] â† chain(ğ‘ ğ‘–ğ‘”[ğ‘–], ğ‘šğ‘ ğ‘”[ğ‘–], ğ‘¤ âˆ’ 1 âˆ’ ğ‘šğ‘ ğ‘”[ğ‘–], PK.seed, ADRS)
		x := sig[i * c.prm.n..(i + 1) * c.prm.n]
		next_chain := chain(c, x, int(msg[i]), int(w - 1 - msg[i]), pkseed, mut adr)!
		assert next_chain.len != 0

		tmp << next_chain
	}
	// copy address to create WOTS+ public key address, wotspkADRS â† ADRS
	mut wots_pkadr := adr.clone()
	// wotspkADRS.setTypeAndClear(WOTS_PK)
	wots_pkadr.set_type_and_clear(.wots_pk)
	// wotspkADRS.setKeyPairAddress(ADRS.getKeyPairAddress())
	// TODO: remove int casts ??
	wots_pkadr.set_keypair_address(adr.get_keypair_address())
	// ğ‘ğ‘˜ğ‘ ğ‘–ğ‘” â† Tğ‘™ğ‘’ğ‘›(PK.seed, wotspkADRS,ğ‘¡ğ‘šğ‘)
	pk_sig := c.tl(pkseed, wots_pkadr, tmp, c.prm.n)!

	return pk_sig
}

fn wots_csum(c &Context, m []u8) u64 {
	mut csum := u64(0)
	t := u32((1 << c.prm.lgw) - 1)

	len1 := c.wots_len1()
	len2 := c.wots_len2()

	mut msg := base_2b(m, c.prm.lgw, len1)

	// for ğ‘– from 0 to ğ‘™ğ‘’ğ‘›1 âˆ’ 1 do
	for i := 0; i < len1; i++ {
		// ğ‘ğ‘ ğ‘¢ğ‘š â† ğ‘ğ‘ ğ‘¢ğ‘š + ğ‘¤ âˆ’ 1 âˆ’ ğ‘šğ‘ ğ‘”[ğ‘–]
		csum += t - msg[i]
	}

	csum <<= u64((8 - ((len2 * c.prm.lgw) & 7)) & 7)

	return csum
}
