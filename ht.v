// Copyright Â© 2024 blackshirt.
// Use of this source code is governed by an MIT license
// that can be found in the LICENSE file.
//
// The SLH-DSA Hypertree module
module pslhdsa

import crypto.internal.subtle

// 7. The SLH-DSA Hypertree
//
// HypertreeSignature is hypertree signature, which is a tree of XMSS trees.
@[noinit]
struct HypertreeSignature {
mut:
	xmss []XmssSignature
}

// new_hypertree creates a new HypertreeSignature from a slice of XmssSignature
@[inline]
fn new_hypertree(xs []XmssSignature) &HypertreeSignature {
	return &HypertreeSignature{
		xmss: xs
	}
}

// ht_size returns the total size of HypertreeSignature h, in bytes.
//
// The size of a hypertree signature is (â„ + ğ‘‘ â‹… ğ‘™ğ‘’ğ‘›) â‹… ğ‘› bytes, where â„ is the height of the hypertree,
// ğ‘‘ is the number of XMSS trees in the hypertree, ğ‘™ğ‘’ğ‘› is the length of each XMSS tree, and ğ‘› is the
// number of bytes in a signature of an XMSS tree.
@[inline]
fn (h &HypertreeSignature) ht_size() int {
	// for every item in xmss, add its size to n
	mut n := 0
	for x in h.xmss {
		n += x.xmss_size()
	}
	return n
}

// bytes returns flatten-ed HypertreeSignature h into bytes array
@[inline]
fn (h &HypertreeSignature) bytes() []u8 {
	mut out := []u8{len: h.ht_size()}
	mut n := 0
	for x in h.xmss {
		copy(mut out[n..n + x.xmss_size()], x.bytes())
		n += x.xmss_size()
	}
	return out
}

// A hypertree signature is (â„ + ğ‘‘ â‹… ğ‘™ğ‘’ğ‘›) â‹… ğ‘› bytes in length and consists of a sequence
// of ğ‘‘ XMSS signatures
@[direct_array_access]
fn parse_hypertree(c Context, sig []u8) !&HypertreeSignature {
	// single xmss signature size
	chunklen := (c.prm.hp + c.wots_len()) * c.prm.n
	if sig.len % chunklen != 0 {
		return error('invalid hypertree signature size')
	}
	mut start := 0
	mut xmss := []XmssSignature{len: c.prm.d}
	for i := 0; i < xmss.len; i++ {
		xmss[i] = parse_xmss_signature(c, sig[start..start + chunklen])!
		start += chunklen
	}
	return new_hypertree(xmss)
}

// 7.1 Hypertree Signature Generation
//
// Algorithm 12 ht_sign(ğ‘€, SK.seed, PK.seed, ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’ğ‘’, ğ‘–ğ‘‘ğ‘¥ğ‘™ğ‘’ğ‘ğ‘“)
// Generates a hypertree signature.
// Input: Message ğ‘€, private seed SK.seed, public seed PK.seed, tree index ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’ğ‘’, leaf index ğ‘–ğ‘‘ğ‘¥ğ‘™ğ‘’ğ‘ğ‘“.
// Output: HT signature SIGğ»ğ‘‡.
// ht_sign generates a hypertree signature.
@[direct_array_access; inline]
fn ht_sign(c &Context, m []u8, skseed []u8, pkseed []u8, mut idxtree TreeIndex, idxleaf_ u32) !&HypertreeSignature {
	mut idxleaf := idxleaf_

	// ADRS â† toByte(0, 32)
	mut adrs := new_address()
	// ADRS.setTreeAddress(ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’ğ‘’)
	// NOTE: this does not handle idxtree > 2^64 - 1, where tree address is 12-bytes long
	adrs.set_tree_address(idxtree)
	// SIGğ‘¡ğ‘šğ‘ â† xmss_sign(ğ‘€, SK.seed,ğ‘–ğ‘‘ğ‘¥ğ‘™ğ‘’ğ‘ğ‘“, PK.seed, ADRS)
	// xmss_sign(c &Context, m []u8, skseed []u8, idx u32, pkseed []u8, mut addr Address) !&XmssSignature
	mut sigtmp := xmss_sign(c, m, skseed, idxleaf, pkseed, mut adrs)!
	// SIGğ»ğ‘‡ â† SIGğ‘¡ğ‘šp
	mut sight := []XmssSignature{len: c.prm.d}
	sight[0] = sigtmp.clone()
	// ğ‘Ÿğ‘œğ‘œğ‘¡ â† xmss_pkFromSig(ğ‘–ğ‘‘ğ‘¥ğ‘™ğ‘’ğ‘ğ‘“, SIGğ‘¡ğ‘šğ‘, ğ‘€, PK.seed, ADRS)
	mut root := xmms_pkfromsig(c, idxleaf, sigtmp, m, pkseed, mut adrs)!

	// for ğ‘— from 1 to ğ‘‘ âˆ’ 1
	for j := u32(1); j < c.prm.d; j++ {
		// ğ‘–ğ‘‘ğ‘¥ğ‘™ğ‘’ğ‘ğ‘“ â† ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’ğ‘’ mod 2^â„â€², â„â€² least significant bits of ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’e
		idxleaf = idxtree.residue(c.prm.hp)
		// remove least significant â„â€² bits from ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’e, ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’ğ‘’ â† ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’ğ‘’ â‰« â„â€²
		idxtree = idxtree.remove_bits(c.prm.hp)
		// ADRS.setLayerAddress(ğ‘—)
		adrs.set_layer_address(j)
		// 10: ADRS.setTreeAddress(ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’ğ‘’)
		adrs.set_tree_address(idxtree)
		// SIGğ‘¡ğ‘šğ‘ â† xmss_sign(ğ‘Ÿğ‘œğ‘œğ‘¡, SK.seed,ğ‘–ğ‘‘ğ‘¥ğ‘™ğ‘’ğ‘ğ‘“, PK.seed, ADRS)
		sigtmp = xmss_sign(c, root, skseed, idxleaf, pkseed, mut adrs)!
		// SIGğ»ğ‘‡ â† SIGğ»ğ‘‡ âˆ¥ SIGğ‘¡ğ‘šp
		sight[j] = sigtmp.clone()
		if j < c.prm.d - 1 {
			// ğ‘Ÿğ‘œğ‘œğ‘¡ â† xmss_pkFromSig(ğ‘–ğ‘‘ğ‘¥ğ‘™ğ‘’ğ‘ğ‘“, SIGğ‘¡ğ‘šğ‘, ğ‘Ÿğ‘œğ‘œğ‘¡, PK.seed, ADRS)
			root = xmms_pkfromsig(c, idxleaf, sigtmp, root, pkseed, mut adrs)!
		}
	}
	return new_hypertree(sight)
}

// 7.2 Hypertree Signature Verification
//
// Algorithm 13 ht_verify(ğ‘€, SIGğ»ğ‘‡, PK.seed, ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’ğ‘’, ğ‘–ğ‘‘ğ‘¥ğ‘™ğ‘’ğ‘ğ‘“, PK.root)
// Verifies a hypertree signature.
// Input: Message ğ‘€,signature SIGğ»ğ‘‡, public seed PK.seed, tree index ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’ğ‘’, leaf index ğ‘–ğ‘‘ğ‘¥ğ‘™ğ‘’ğ‘ğ‘“, HT public key PK.root.
// ht_verify verifies a hypertree signature.
@[direct_array_access; inline]
fn ht_verify(c &Context, m []u8, sight &HypertreeSignature, pkseed []u8, mut idxtree TreeIndex, idxleaf_ u32, pkroot []u8) !bool {
	// mut idxtree := idxtree
	mut idxleaf := idxleaf_

	// ADRS â† toByte(0, 32)
	mut adrs := new_address()
	// ADRS.setTreeAddress(ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’ğ‘’)
	// NOTE: this does not handle idxtree > 2^64 - 1, where tree address is 12-bytes long
	adrs.set_tree_address(idxtree)
	// SIGğ‘¡ğ‘šğ‘ â† SIGğ»ğ‘‡.getXMSSSignature(0) â–· SIGğ»ğ‘‡[0 âˆ¶ (â„â€² + ğ‘™ğ‘’ğ‘›) â‹… ğ‘›]
	// mut sigtmp := sight[0..(c.prm.hp + c.wots_len()) * c.prm.n].clone()
	mut sigtmp := sight.xmss[0]
	// ğ‘›ğ‘œğ‘‘ğ‘’ â† xmss_pkFromSig(ğ‘–ğ‘‘ğ‘¥ğ‘™ğ‘’ğ‘ğ‘“, SIGğ‘¡ğ‘šğ‘, ğ‘€, PK.seed, ADRS)
	mut node := xmms_pkfromsig(c, idxleaf, sigtmp, m, pkseed, mut adrs)!

	// for ğ‘— from 1 to ğ‘‘ âˆ’ 1 do
	for j := u32(1); j < c.prm.d; j++ {
		// ğ‘–ğ‘‘ğ‘¥ğ‘™ğ‘’ğ‘ğ‘“ â† ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’ğ‘’ mod 2^â„â€², â„â€² least significant bits of ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’e
		idxleaf = idxtree.residue(c.prm.hp)
		// remove least significant â„â€² bits from ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’e, ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’ğ‘’ â† ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’ğ‘’ â‰« â„â€²
		idxtree = idxtree.remove_bits(c.prm.hp)
		// ADRS.setLayerAddress(ğ‘—)
		adrs.set_layer_address(j)
		// 10: ADRS.setTreeAddress(ğ‘–ğ‘‘ğ‘¥ğ‘¡ğ‘Ÿğ‘’ğ‘’)
		adrs.set_tree_address(idxtree)

		// SIGğ‘¡ğ‘šğ‘ â† SIGğ»ğ‘‡.getXMSSSignature(ğ‘—) â–· SIGğ»ğ‘‡[ğ‘— â‹… (â„â€² + ğ‘™ğ‘’ğ‘›) â‹… ğ‘› âˆ¶ (ğ‘— + 1)(â„â€² + ğ‘™ğ‘’ğ‘›) â‹… ğ‘›]
		sigtmp = sight.xmss[j]

		// ğ‘›ğ‘œğ‘‘ğ‘’ â† xmss_pkFromSig(ğ‘–ğ‘‘ğ‘¥ğ‘™ğ‘’ğ‘ğ‘“, SIGğ‘¡ğ‘šğ‘, ğ‘›ğ‘œğ‘‘ğ‘’, PK.seed, ADRS)
		node = xmms_pkfromsig(c, idxleaf, sigtmp, node, pkseed, mut adrs)!
	}

	// if ğ‘›ğ‘œğ‘‘ğ‘’ = PK.root { return true }
	return subtle.constant_time_compare(node, pkroot) == 1
}
