module pslhdsa

// 6. eXtended Merkle Signature Scheme (XMSS)
// XMSS extends the WOTS+ signature scheme into one that can sign multiple messages.
// An XMSS signature is (â„â€² + ğ‘™ğ‘’ğ‘›) â‹… ğ‘› bytes in length and consists of a WOTS+ signature and an
// authentication path

// 6.1 Generating a Merkle Hash Tree
// Algorithm 9 xmss_node(SK.seed, ğ‘–, ğ‘§, PK.seed, ADRS)
// Computes the root of a Merkle subtree of WOTS+ public keys.
// Input: Secret seed SK.seed, target node index ğ‘–, target node height ğ‘§, public seed PK.seed,
// address ADRS.
// Output: ğ‘›-byte root ğ‘›ğ‘œğ‘‘e
fn xmss_node(c Context, sk_seed []u8, i int, z int, pk_seed []u8, mut addr Address) ![]u8 {
	if z > c.prm.hp || i >= (1 << (c.prm.hp - z)) {
		return error('bad xmss_node params')
	}
	if z == 0 {
		// ADRS.setTypeAndClear(WOTS_HASH)
		addr.set_type_and_clear(.wots_hash)
		// ADRS.setKeyPairAddress(ğ‘–)
		addr.set_keypair_address(u32(i))
		// ğ‘›ğ‘œğ‘‘ğ‘’ â† wots_pkGen(SK.seed, PK.seed, ADRS)
		// wots_pkgen(c Context, sk_seed []u8, pk_seed []u8, mut addr Address)
		node := wots_pkgen(c, sk_seed, pk_seed, mut addr)!
		return node
	}
	// otherwise
	// ğ‘™ğ‘›ğ‘œğ‘‘ğ‘’ â† xmss_node(SK.seed, 2ğ‘–, ğ‘§ âˆ’ 1, PK.seed, ADRS)
	lnode := xmss_node(c, sk_seed, 2 * i, z - 1, pk_seed, mut addr)!
	// ğ‘Ÿğ‘›ğ‘œğ‘‘ğ‘’ â† xmss_node(SK.seed, 2ğ‘– + 1, ğ‘§ âˆ’ 1, PK.seed, ADRS)
	rnode := xmss_node(c, sk_seed, 2 * i + 1, z - 1, pk_seed, mut addr)!
	// 8: ADRS.setTypeAndClear(TREE)
	addr.set_type_and_clear(.tree)
	// 9: ADRS.setTreeHeight(ğ‘§)
	addr.set_tree_height(u32(z))
	// 10: ADRS.setTreeIndex(ğ‘–)
	addr.set_tree_index(u32(i))

	// 11: ğ‘›ğ‘œğ‘‘ğ‘’ â† H(PK.seed, ADRS, ğ‘™ğ‘›ğ‘œğ‘‘ğ‘’ âˆ¥ ğ‘Ÿğ‘›ğ‘œğ‘‘ğ‘’)
	mut gab := []u8{}
	gab << lnode
	gab << rnode
	node := c.h(pk_seed, addr, gab)!

	return node
}

// 6.2 Generating an XMSS Signature
//
// Algorithm 10 xmss_sign(ğ‘€, SK.seed, ğ‘–ğ‘‘ğ‘¥, PK.seed, ADRS)
// Generates an XMSS signature.
// Input: ğ‘›-byte message ğ‘€, secret seed SK.seed, index ğ‘–ğ‘‘ğ‘¥, public seed PK.seed,
// address ADRS.
// Output: XMSS signature SIGğ‘‹ğ‘€ğ‘†ğ‘† = (ğ‘ ğ‘–ğ‘” âˆ¥ AUTH).
fn xmms_sign(c Context, m []u8, sk_seed []u8, idx int, pk_seed []u8, mut addr Address) ![]u8 {
	assert m.len == c.prm.n
	assert idx >= 0
	assert idx < (1 << c.prm.hp)

	mut auth := []u8{}
	// build authentication path
	for j := 0; j < c.prm.hp; j++ {
		// ğ‘˜ â† âŒŠğ‘–ğ‘‘ğ‘¥/2ğ‘—âŒ‹ âŠ• 1
		k := (idx >> j) ^ 0x01
		// 3: AUTH[ğ‘—] â† xmss_node(SK.seed, ğ‘˜, ğ‘—, PK.seed, ADRS)
		auth_j := xmss_node(c, sk_seed, k, j, pk_seed, mut addr)!
		auth << auth_j
	}
	// ADRS.setTypeAndClear(WOTS_HASH)
	addr.set_type_and_clear(.wots_hash)
	// 6: ADRS.setKeyPairAddress(ğ‘–ğ‘‘ğ‘¥)
	addr.set_keypair_address(u32(idx))
	// 7: ğ‘ ğ‘–ğ‘” â† wots_sign(ğ‘€, SK.seed, PK.seed, ADRS)
	sig := wots_sign(c, m, sk_seed, pk_seed, mut addr)!
	// 8: SIGğ‘‹ğ‘€ğ‘†ğ‘† â† ğ‘ ğ‘–ğ‘” âˆ¥ AUTH
	mut sig_xmss := []u8{}
	sig_xmss << sig
	sig_xmss << auth

	return sig_xmss
}

// 6.3 Computing an XMSS Public Key From a Signature
//
// Algorithm 11 xmss_pkFromSig(ğ‘–ğ‘‘ğ‘¥, SIGğ‘‹ğ‘€ğ‘†ğ‘†, ğ‘€, PK.seed, ADRS)
// Computes an XMSS public key from an XMSS signature.
// Input: Index ğ‘–ğ‘‘ğ‘¥, XMSS signature SIGğ‘‹ğ‘€ğ‘†ğ‘† = (ğ‘ ğ‘–ğ‘” âˆ¥ AUTH), ğ‘›-byte message, public seed PK.seed, address ADRS.
// Output: ğ‘›-byte root value ğ‘›ğ‘œğ‘‘ğ‘’[0].
fn xmms_pkfromsig(c Context, idx int, sig_xmss []u8, m []u8, pk_seed []u8, mut addr Address) ![]u8 {
	assert idx >= 0
	assert m.len == c.prm.n
	assert sig_xmss.len == (c.prm.wots_len() + c.prm.hp) * c.prm.n

	// compute WOTS+ pk from WOTS+ ğ‘ ğ‘–g, ADRS.setTypeAndClear(WOTS_HASH)
	addr.set_type_and_clear(.wots_hash)
	// ADRS.setKeyPairAddress(ğ‘–ğ‘‘ğ‘¥)
	addr.set_keypair_address(u32(idx))
	// SIGğ‘‹ğ‘€ğ‘†ğ‘†[0 âˆ¶ ğ‘™ğ‘’ğ‘› â‹… ğ‘›], ğ‘ ğ‘–ğ‘” â† SIGğ‘‹ğ‘€ğ‘†ğ‘†.getWOTSSig()
	sig := sig_xmss[..c.prm.wots_len() * c.prm.n]
	// : AUTH â† SIGğ‘‹ğ‘€ğ‘†ğ‘†.getXMSSAUTH() â–· SIGğ‘‹ğ‘€ğ‘†ğ‘†[ğ‘™ğ‘’ğ‘› â‹… ğ‘› âˆ¶ (ğ‘™ğ‘’ğ‘› + â„â€²) â‹… ğ‘›]
	auth := sig_xmss[c.prm.wots_len() * c.prm.n..]

	// ğ‘›ğ‘œğ‘‘ğ‘’[0] â† wots_pkFromSig(ğ‘ ğ‘–ğ‘”, ğ‘€, PK.seed, ADRS)
	mut node_0 := wots_pkfromsig(c, sig, m, pk_seed, mut addr)!

	// compute root from WOTS+ pk and AUTH
	// ADRS.setTypeAndClear(TREE)
	addr.set_type_and_clear(.tree)
	// ADRS.setTreeIndex(ğ‘–ğ‘‘ğ‘¥)
	addr.set_tree_index(u32(idx))

	for k := 0; k <= c.prm.hp - 1; k++ {
		// ADRS.setTreeHeight(ğ‘˜ + 1)
		addr.set_tree_height(u32(k + 1))
		if (idx >> k) % 2 == 0 {
			// 11: ADRS.setTreeIndex(ADRS.getTreeIndex()/2)
			addr.set_tree_index(u32(addr.get_tree_index() >> 1))
			// 12: ğ‘›ğ‘œğ‘‘ğ‘’[1] â† H(PK.seed, ADRS, ğ‘›ğ‘œğ‘‘ğ‘’[0] âˆ¥ AUTH[ğ‘˜])
			m_auth_k := auth[k * c.prm.n..(k + 1) * c.prm.n]
			mut m2 := []u8{}
			m2 << node_0
			m2 << m_auth_k
			node_1 := c.h(pk_seed, addr, m2)!
			node_0 = unsafe { node_1 }
		} else {
			// ADRS.setTreeIndex((ADRS.getTreeIndex() âˆ’ 1)/2)
			ix := (addr.get_tree_index() - 1) >> 1
			addr.set_tree_index(ix)
			// ğ‘›ğ‘œğ‘‘ğ‘’[1] â† H(PK.seed, ADRS, AUTH[ğ‘˜] âˆ¥ ğ‘›ğ‘œğ‘‘ğ‘’[0])
			m_auth_k := auth[k * c.prm.n..(k + 1) * c.prm.n]
			mut m2 := []u8{}
			m2 << m_auth_k
			m2 << node_0
			node_1 := c.h(pk_seed, addr, m2)!
			node_0 = unsafe { node_1 }
		}
	}
	return node_0
}
