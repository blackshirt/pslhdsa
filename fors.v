module pslhdsa

// 8. Forest of Random Subsets (FORS)

// 8.1 Generating FORS Secret Values
//
// Algorithm 14 fors_skGen(SK.seed, PK.seed, ADRS, ğ‘–ğ‘‘ğ‘¥)
// Generates a FORS private-key value.
// Input: Secret seed SK.seed, public seed PK.seed, address ADRS, secret key index ğ‘–ğ‘‘ğ‘¥.
// Output: ğ‘›-byte FORS private-key value.
fn fors_skgen(c Context, sk_seed []u8, pk_seed []u8, addr Address, idx int) ![]u8 {
	// idx >=0
	// copy address to create key generation address
	mut sk_addr := addr.clone()
	// skADRS.setTypeAndClear(FORS_PRF)
	sk_addr.set_type_and_clear(.fors_prf)
	// 3: skADRS.setKeyPairAddress(ADRS.getKeyPairAddress())
	sk_addr.set_keypair_address(addr.get_keypair_address())
	// skADRS.setTreeIndex(ğ‘–ğ‘‘ğ‘¥)
	sk_addr.set_tree_index(u32(idx))

	// return PRF(PK.seed, SK.seed,skADRS)
	return c.prf(pk_seed, sk_seed, sk_addr)
}

// 8.2 Generating a Merkle Hash Tree
//
// Algorithm 15 fors_node(SK.seed, ğ‘–, ğ‘§, PK.seed, ADRS)
// Computes the root of a Merkle subtree of FORS public values.
// Input: Secret seed SK.seed, target node index ğ‘–, target node height ğ‘§, public seed PK.seed, address ADRS.
// Output: ğ‘›-byte root ğ‘›ğ‘œğ‘‘ğ‘’.
fn fors_node(c Context, sk_seed []u8, i int, z int, pk_seed []u8, addr_ Address) ![]u8 {
	// if z > ctx.a or i >= ctx.k * 2**(ctx.a - z):
	if z > c.a || i >= c.k * (1 << (c.a - z)) {
		return error('Bad fors_node params')
		// return empty bytes instead ?
		// return []u8{}
	}
	mut addr := addr_.clone()
	if z == 0 {
		// ğ‘ ğ‘˜ â† fors_skGen(SK.seed, PK.seed, ADRS,ğ‘–)
		skey := fors_skgen(c, sk_seed, pk_seed, addr, i)!
		// 3: ADRS.setTreeHeight(0)
		addr.set_tree_height(u32(0))
		// 4: ADRS.setTreeIndex(ğ‘–)
		addr.set_tree_index(u32(i))
		// 5: ğ‘›ğ‘œğ‘‘ğ‘’ â† F(PK.seed, ADRS, ğ‘ ğ‘˜)
		node := c.f(pk_seed, addr, skey)!
		return node
	}
	// else
	// ğ‘™ğ‘›ğ‘œğ‘‘ğ‘’ â† fors_node(SK.seed, 2ğ‘–, ğ‘§ âˆ’ 1, PK.seed, ADRS)
	lnode := fors_node(c, sk_seed, 2 * i, z - 1, pk_seed, addr)!
	// 8: ğ‘Ÿğ‘›ğ‘œğ‘‘ğ‘’ â† fors_node(SK.seed, 2ğ‘– + 1, ğ‘§ âˆ’ 1, PK.seed, ADRS)
	rnode := fors_node(c, sk_seed, 2 * i + 1, z - 1, pk_seed, addr)!
	// 9: ADRS.setTreeHeight(ğ‘§)
	addr.set_tree_height(u32(z))
	// 10: ADRS.setTreeIndex(ğ‘–)
	addr.set_tree_index(u32(i))
	// 11: ğ‘›ğ‘œğ‘‘ğ‘’ â† H(PK.seed, ADRS, ğ‘™ğ‘›ğ‘œğ‘‘ğ‘’ âˆ¥ ğ‘Ÿğ‘›ğ‘œğ‘‘ğ‘’)
	mut m2 := []u8{}
	m2 << lnode
	m2 << rnode
	node := c.h(pk_seed, addr, m2)!

	return node
}

// 8.3 Generating a FORS Signature
//
// Algorithm 16 fors_sign(ğ‘šğ‘‘, SK.seed, PK.seed, ADRS)
// Generates a FORS signature.
// Input: Message digest ğ‘šğ‘‘, secret seed SK.seed, address ADRS, public seed PK.seed.
// Output: FORS signature SIGğ¹ğ‘‚ğ‘…ğ‘†.
fn fors_sign(c Context, md []u8, sk_seed []u8, pk_seed []u8, addr_ Address) ![]u8 {
	assert md.len == cdiv(c.k * c.a, 8)
	mut addr := addr_.clone()
	// initialize SIGğ¹ğ‘‚ğ‘…ğ‘† as a zero-length byte string
	mut sig_fors := []u8{}
	//  ğ‘–ğ‘›ğ‘‘ğ‘–ğ‘ğ‘’ğ‘  â† base_2b(ğ‘šğ‘‘, ğ‘, ğ‘˜)
	indices := base_2exp_b(md, c.a, c.k)

	// compute signature elements
	for i := 0; i < c.k; i++ {
		// fors_skGen(SK.seed, PK.seed, ADRS,ğ‘– â‹… 2^ğ‘ + ğ‘–ğ‘›ğ‘‘ğ‘–ğ‘ğ‘’ğ‘ [ğ‘–])
		fors_item := fors_skgen(c, sk_seed, pk_seed, addr, i << c.a + int(indices[i]))!
		sig_fors << fors_item

		// compute auth path
		mut auth := []u8{}
		// for ğ‘— from 0 to ğ‘ âˆ’ 1 do
		for j := 0; j < c.a; j++ {
			// s â† âŒŠğ‘–ğ‘›ğ‘‘ğ‘–ğ‘ğ‘’ğ‘ [ğ‘–]/2^ğ‘—âŒ‹ âŠ• 1
			s := (indices[i] >> j) ^ 1
			// AUTH[ğ‘—] â† fors_node(SK.seed,ğ‘– * 2^(ğ‘âˆ’ğ‘—) + ğ‘ , ğ‘—, PK.seed, ADRS)
			idx := u32(i) << (c.a - j) + s
			auth_j := fors_node(c, sk_seed, int(idx), j, pk_seed, addr)!
			auth << auth_j
		}
		// SIGğ¹ğ‘‚ğ‘…ğ‘† â† SIGğ¹ğ‘‚ğ‘…ğ‘† âˆ¥ AUTH
		sig_fors << auth
	}
	return sig_fors
}

// 8.4 Computing a FORS Public Key From a Signature
//
// Algorithm 17 fors_pkFromSig(SIGğ¹ğ‘‚ğ‘…ğ‘†, ğ‘šğ‘‘, PK.seed, ADRS)
// Computes a FORS public key from a FORS signature.
// Input: FORS signature SIGğ¹ğ‘‚ğ‘…ğ‘†, message digest ğ‘šğ‘‘, public seed PK.seed, address ADRS.
// Output: FORS public key
fn fors_pkfromsig(c Context, sig_fors []u8, md []u8, pk_seed []u8, addr_ Address) ![]u8 {
	// assert sig_fors.len == c.k * (c.a + 1) * c.n
	assert md.len == cdiv(c.k * c.a, 8)
	mut addr := addr_.clone()
	// ğ‘–ğ‘›ğ‘‘ğ‘–ğ‘ğ‘’ğ‘  â† base_2b(ğ‘šğ‘‘, ğ‘, ğ‘˜)
	indices := base_2exp_b(md, c.a, c.k)
	mut node := [][]u8{len: 2}
	mut root := []u8{}
	for i := 0; i < c.k; i++ {
		// ğ‘ ğ‘˜ â† SIGğ¹ğ‘‚ğ‘…ğ‘†.getSK(ğ‘–), SIGğ¹ğ‘‚ğ‘…ğ‘†[ğ‘– â‹… (ğ‘ + 1) â‹… ğ‘› âˆ¶ (ğ‘– â‹… (ğ‘ + 1) + 1) â‹… ğ‘›]
		start := i * (c.a + 1) * c.n
		end := (i * (c.a + 1) + 1) * c.n
		skey := sig_fors[start..end]
		// compute leaf
		// ADRS.setTreeHeight(0)
		addr.set_tree_height(0)
		// ADRS.setTreeIndex(ğ‘– â‹… 2^ğ‘ + ğ‘–ğ‘›ğ‘‘ğ‘–ğ‘ğ‘’ğ‘ [ğ‘–])
		tree_idx := u32(i) << c.a + indices[i]
		addr.set_tree_index(tree_idx)
		// ğ‘›ğ‘œğ‘‘ğ‘’[0] â† F(PK.seed, ADRS, ğ‘ ğ‘˜)
		node[0] = c.f(pk_seed, addr, skey)!

		// compute root from leaf and AUTH
		// ğ‘ğ‘¢ğ‘¡â„ â† SIGğ¹ğ‘‚ğ‘…ğ‘†.getAUTH(ğ‘–) â–· SIGğ¹ğ‘‚ğ‘…ğ‘†[(ğ‘– â‹… (ğ‘ + 1) + 1) â‹… ğ‘› âˆ¶ (ğ‘– + 1) â‹… (ğ‘ + 1) â‹… ğ‘›]
		auth_start := (i * (c.a + 1) + 1) * c.n
		auth_end := (i + 1) * (c.a + 1) * c.n
		auth := sig_fors[auth_start..auth_end]
		for j := 0; j < c.a; j++ {
			// ADRS.setTreeHeight(ğ‘— + 1)
			addr.set_tree_height(u32(j + 1))
			// if âŒŠğ‘–ğ‘›ğ‘‘ğ‘–ğ‘ğ‘’ğ‘ [ğ‘–]/2^ğ‘—âŒ‹ is even
			if (indices[i] >> j) & 1 == 0 {
				// ADRS.setTreeIndex(ADRS.getTreeIndex()/2)
				addr.set_tree_index(addr.get_tree_index() >> 1)
				// ğ‘›ğ‘œğ‘‘ğ‘’[1] â† H(PK.seed, ADRS, ğ‘›ğ‘œğ‘‘ğ‘’[0] âˆ¥ ğ‘ğ‘¢ğ‘¡â„[ğ‘—])
				mut msi := []u8{}
				auth_j := auth[j * c.n..(j + 1) * c.n]
				// auth[j * c.n..(j + 1) * c.n]
				msi << node[0]
				msi << auth_j
				node[1] = c.h(pk_seed, addr, msi)!
			} else {
				// ADRS.setTreeIndex((ADRS.getTreeIndex() âˆ’ 1)/2)
				addr.set_tree_index((addr.get_tree_index() - 1) >> 1)
				// 15: ğ‘›ğ‘œğ‘‘ğ‘’[1] â† H(PK.seed, ADRS, ğ‘ğ‘¢ğ‘¡â„[ğ‘—] âˆ¥ ğ‘›ğ‘œğ‘‘ğ‘’[0])
				mut msi := []u8{}
				// msi << auth[j * c.n..(j + 1) * c.n]
				auth_j := auth[j * c.n..(j + 1) * c.n]
				msi << auth_j
				msi << node[0]
				node[1] = c.h(pk_seed, addr, msi)!
			}
			// ğ‘›ğ‘œğ‘‘ğ‘’[0] â† ğ‘›ğ‘œğ‘‘ğ‘’[1]
			node[0] = unsafe { node[1] }
		}
		// ğ‘Ÿğ‘œğ‘œğ‘¡[ğ‘–] â† ğ‘›ğ‘œğ‘‘ğ‘’[0]
		root << node[0]
	}
	// copy address to create a FORS public-key address, 	forspkADRS â† ADRS â–·
	mut fors_pkaddr := addr.clone()
	// 22: forspkADRS.setTypeAndClear(FORS_ROOTS)
	fors_pkaddr.set_type_and_clear(.fors_roots)
	// 23: forspkADRS.setKeyPairAddress(ADRS.getKeyPairAddress())
	fors_pkaddr.set_keypair_address(u32(addr.get_keypair_address()))

	// compute the FORS public key
	// 24: ğ‘ğ‘˜ â† Tğ‘˜(PK.seed, forspkADRS, ğ‘Ÿğ‘œğ‘œğ‘¡) â–·
	pk := c.tlen(pk_seed, fors_pkaddr, root)!

	return pk
}
